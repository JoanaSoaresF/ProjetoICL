PARSER_BEGIN(Parser0)

import AST.*;
import java.util.HashMap;
import java.util.Map;

public class Parser0 {

}

PARSER_END(Parser0)

SKIP :{
    " "
    | "\t"
    | "\r"
    |"\n"
}

TOKEN :{
    <NEW : "new">
    | <FUN : "fun">
    | <FUN_ARROW : "->">
    |  <ASSIGN : ":=">
    |  <DESREF : "!">
    |  <IF : "if">
    |  <THEN : "then">
    |  <ELSE : "else">
    |  <WHILE : "while">
    |  <DO : "do">
    |  <PRINT : "println">
    |  <TRUE : "true">
    |  <FALSE : "false">
    |  <AND : "&&">
    |  <OR: "||">
    |  <EQ : "==">
    |  <DIF : "~=">
    |  <GET : ">=">
    |  <LET : "<=">
    |  <GT : ">">
    |  <LT : "<">
    |  <NEG : "~">
    |  <DEF : "def">
    |  <IN : "in">
    |  <END : "end">
    |  <EQ_DEF : "=">
    |  < Id: ["a"-"z","A"-"Z"] ( ["a"-"z","A"-"Z","0"-"9"] )* >
    |  < Num: (["0"-"9"]) + >
    |  < PLUS : "+" >
    |  < MINUS : "-">
    |  < MUL : "*">
    |  < DIV : "/">
    |  < LPAR : "(" >
    |  < RPAR : ")" >
    |  < EL: ";;" >
    |  <SEQ: ";">
 }


ASTNode Start() :
{
 ASTNode t1;
 }
{   t1 = Seq() <EL>
	{ return t1; }}


ASTNode Seq():
{ ASTNode t1, t2; }
{
   t1 = Assign() (<SEQ> t2=Assign() {t1 = new ASTSeq(t1, t2);})*
   { return t1;}
}

ASTNode Assign():
{ ASTNode t1, t2; }
{
   t1 = BoolAdd() (<ASSIGN> t2=BoolAdd() {t1 = new ASTAssign(t1, t2);})?
   { return t1;}
}

ASTNode BoolAdd() :
{ ASTNode t1, t2;
 Token tok;}
{ t1 = BoolMult() (tok = <OR>  t2 = BoolMult() {t1 = new ASTOr(t1, t2);})*
    {return t1;}
}

ASTNode BoolMult() :
{ ASTNode t1, t2;}
{    t1 = BoolRel() ("&&" t2 = BoolRel()
                            {t1 = new ASTAnd(t1, t2);}
                    )*
     {return t1;}
}


ASTNode BoolRel() :
{ ASTNode t1;
 ASTNode t2;
 Token t;}
{  t1 = Exp() ((t = <EQ> | t = <GT> | t = <GET> | t = <LT> | t = <LET> | t = <DIF>)t2 = Exp()
                {t1 = new ASTRelationOperands(t1,t.image, t2); }
                )?

  {return t1;}
}

ASTNode Exp() :
{ASTNode t1,t2;
Token tok;}
{     t1 = Term() (( tok=<PLUS> | tok=<MINUS> ) t2=Term()
                     {
                             if (tok.kind == PLUS){
                                 t1 = new ASTAdd(t1,t2);
                             }else{
                                 t1 = new ASTSub(t1,t2);
                             }
                     }
                   ) *
     { return t1; }
}

ASTNode Term() :
{ASTNode t1,t2;Token tok;}
{     t1=Fact() ( ( tok = <MUL> | tok = <DIV> ) t2=Fact()
                     { if (tok.kind == MUL)
                              t1 = new ASTMult(t1,t2);
                        else  t1 = new ASTDiv(t1,t2);   } ) *

   { return t1; }
}


ASTNode Fact() :
{ ASTNode t1;
 ASTNode t2;
 ASTNode t3;
 ASTNode e;
 Token tok;}
{   (tok=<Num> { t1 = new ASTNum(Integer.parseInt(tok.image)); }
   | tok = <Id> { t1 = new ASTId(tok.image);}
   | tok = <TRUE> { t1 = new ASTBoolean(true);}
   | tok = <FALSE> { t1 = new ASTBoolean(false);}
   | tok = <DESREF> t1 = Fact() {t1 = new ASTDeref(t1);}
   | <LPAR> t1=Seq() <RPAR>
   | <NEG> t1 = Fact()  { t1 = new ASTNegation(t1);}
   | tok = <MINUS> t1 =Fact() {t1 = new ASTUMinus(t1);}
   | tok = <DEF> { Token n;
                   Map<String, ASTNode> inits = new HashMap<String, ASTNode>();
                   ASTNode t;}
                (n=<Id> <EQ_DEF> e = BoolAdd() {inits.put(n.image, e);})+
            <IN>
                t = Seq()
            <END> {t1 = new ASTDef(t, inits);}
   | tok = <IF> t1 = BoolAdd() <THEN> t2 = Seq() <ELSE> t3=Seq() <END>
            {t1 =  new ASTIf(t1, t2, t3);}
   | tok = <WHILE> t1 = BoolAdd() <DO> t2=Seq() <END>
            {t1 =  new ASTWhile(t1, t2);}
   | tok = <PRINT> t1 = Fact()
             {t1 =  new ASTPrint(t1);}
   | tok = <NEW> t1 = Fact()
            {t1 =  new ASTNew(t1);}

   )
  { return t1; }  }